{
  lib,
  stdenvNoCC,
  makeWrapper,
  quickshell,
  wlr-randr,
  wlsunset,
  networkmanager,
  pulseaudio,
  wl-clipboard,
  coreutils,
  procps,
  socat,
  pipewire,
  python3,
  curl,
  libnotify,
  glib,
  grim,
  slurp,
  wf-recorder,
  ffmpeg,
  git,
  qt6,
  sylvarisParts ? { },
  commit ? "unknown",
}:

let
  python = python3.withPackages (ps: [ ps.cryptography ]);
  partTools = {
    bar = [
      pulseaudio
      pipewire
      python
      libnotify
    ];
    center = [
      wlr-randr
      networkmanager
      wlsunset
      pulseaudio
      pipewire
      python
      libnotify
      wl-clipboard
    ];
    clock = [
      pipewire
      python
      libnotify
      curl
    ];
    deck = [ ];
    diver = [
      pipewire
      python
      libnotify
    ];
    media = [
      pulseaudio
      pipewire
      python
    ];
    notify = [ ];
    pad = [ ];
    paper = [ ];
    power = [ ];
    lock = [ glib ];
    polkit = [ ];
    clip = [ wl-clipboard ];
    island = [ ];
    viewer = [
      wl-clipboard
      glib
    ];
    access = [ ];
    fatest = [ ];
    rgb = [ python ];
    plugins = [ git ];
    sync = [
      python
      procps
    ];
    capture = [
      grim
      slurp
      wf-recorder
      ffmpeg
      wl-clipboard
      libnotify
      pulseaudio
    ];
    switcher = [ ];
    settings = [
      wlsunset
      pipewire
      python
      libnotify
      curl
    ];
    theme = [ ];
  };
  tools = lib.unique (
    [
      quickshell
      coreutils
      procps
      socat
    ]
    ++ lib.concatLists (lib.mapAttrsToList (name: lib.optionals (sylvarisParts.${name} or true)) partTools)
  );
in
stdenvNoCC.mkDerivation {
  pname = "sylvaris";
  version = "0.2.0";

  src = lib.fileset.toSource {
    root = ../.;
    fileset = lib.fileset.unions [
      ../shell
      ../bin
      ../share
    ];
  };

  nativeBuildInputs = [ makeWrapper ];

  installPhase = ''
    runHook preInstall
    mkdir -p $out/share/sylvaris
    cp -r shell/. $out/share/sylvaris/
    printf '%s\n' ${lib.escapeShellArg commit} > $out/share/sylvaris/COMMIT
    install -Dm755 bin/sylvaris $out/bin/sylvaris
    install -Dm644 share/applications/sylvaris-viewer.desktop $out/share/applications/sylvaris-viewer.desktop
    wrapProgram $out/bin/sylvaris \
      --set-default SYLVARIS_DIR $out/share/sylvaris \
      --prefix PATH : ${lib.makeBinPath tools} \
      --prefix QML_IMPORT_PATH : ${qt6.qtmultimedia}/lib/qt-6/qml \
      --prefix QT_PLUGIN_PATH : ${qt6.qtmultimedia}/lib/qt-6/plugins:${qt6.qtimageformats}/lib/qt-6/plugins
    runHook postInstall
  '';

  passthru.parts = builtins.attrNames partTools;

  meta = {
    description = "Modular Quickshell desktop shell for Hyprland, niri and sway";
    license = lib.licenses.mit;
    platforms = lib.platforms.linux;
    mainProgram = "sylvaris";
  };
}
